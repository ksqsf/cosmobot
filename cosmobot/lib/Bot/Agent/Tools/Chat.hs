{-|
Module      : Bot.Agent.Tools.Chat
Description : Agent tools for chat IO and chat metadata
Stability   : experimental
-}

module Bot.Agent.Tools.Chat
  ( queryChatLogTool
  , queryCurrentSenderChatLogTool
  , sendReplyTool
  , sendFileTool
  , mentionUserTool
  , senderMemberInfoTool
  , memberInfoTool
  , userAvatarTool
  , listGroupMembersTool
  , currentMessageInfoTool
  )
where

import Bot.Agent.Tools.Common
import Bot.Agent.Tool
import Bot.Agent.Types
import Bot.Core.Message
import qualified Bot.Core.ReplyBody as ReplyBody
import qualified Bot.Effect.Chat as Chat
import qualified Bot.Effect.ChatLog as ChatLog
import Bot.Prelude
import qualified Data.Aeson as Aeson
import qualified Data.Aeson.Key as Key
import qualified Data.Aeson.Types as AesonTypes
import qualified Data.Text as Text
import Data.Time (UTCTime)

queryChatLogTool :: ChatLog.ChatLog :> es => Tool (Eff es)
queryChatLogTool =
  tagged [chatTag]
  . withDescription "Return recent messages recorded in the current chat, optionally filtered by exact sender id. Results are in chronological order and include timestamps, sender ids, message ids, image urls, and text. Use since or before to page through time."
  $ tool "chat_log"
      ( requiredInt "limit" "Maximum number of recent messages to return."
      , optionalText "sender" "Optional exact sender id to include."
      , withDefault False (optionalBoolean "include_bot_messages" "Whether to include bot messages. Defaults to false.")
      , optionalArgument (fieldDateTime "since" "Return messages strictly after this ISO-8601 UTC timestamp.")
      , optionalArgument (fieldDateTime "before" "Return messages strictly before this ISO-8601 UTC timestamp.")
      )
      \limit sender includeBotMessages since before -> do
        context <- askToolContext
        let senderFilter = do
              value <- Text.strip <$> sender
              guard (not (Text.null value))
              pure value
        case chatLogTimeRange since before of
          Left err ->
            pure (argumentFailure err)
          Right timeRange -> do
            entries <- ChatLog.queryChat context.message senderFilter (max 0 limit) includeBotMessages timeRange
            pure (toolText (jsonText (map chatLogToolEntry entries)))

queryCurrentSenderChatLogTool :: ChatLog.ChatLog :> es => Tool (Eff es)
queryCurrentSenderChatLogTool =
  tagged [chatTag]
  . withDescription "Return messages from the current sender whose text matches any keyword group. scope=chat searches only the current chat; scope=global searches all chats on the current platform. Each keyword group is matched as a SQL LIKE pattern with '%' between its terms. Results are newest first and limited to at most 100."
  $ tool "sender_log"
      ( requiredArgument (fieldTextArrayArray "keywords" "Keyword groups. Each inner array is joined with '%' and wrapped with '%' for ordered fuzzy matching.")
      , validateArgument validSenderLogLimit
          (requiredArgument (fieldIntegerMax "limit" 100 "Maximum number of matching messages to return. Must be <= 100."))
      , validateArgument parseSenderChatLogScope
          (withDefault "chat" (optionalArgument
            ("scope", Aeson.object
          [ "type" Aeson..= ("string" :: Text)
          , "enum" Aeson..= (["chat", "global"] :: [Text])
          , "description" Aeson..= ("Search the current chat or all chats on the current platform. Defaults to chat." :: Text)
          ])))
      , optionalArgument (fieldDateTime "since" "Return messages strictly after this ISO-8601 UTC timestamp.")
      , optionalArgument (fieldDateTime "before" "Return messages strictly before this ISO-8601 UTC timestamp.")
      )
      \keywords limit scope since before -> do
        context <- askToolContext
        case currentSenderChatLogScopeError scope context.message of
          Just err ->
              pure (argumentFailure err)
          Nothing ->
            case chatLogTimeRange since before of
              Left err ->
                pure (argumentFailure err)
              Right timeRange -> do
                entries <- ChatLog.queryCurrentSenderChatLog context.message scope keywords limit timeRange
                pure (toolText (jsonText (map chatLogToolEntry entries)))

sendReplyTool :: Chat.Chat :> es => Tool (Eff es)
sendReplyTool =
  tagged [chatTag]
  . withDescription "Send a reply message to the same chat as the current user message. Supports text and image URLs. Use image_urls when the user asks you to send an image found or generated elsewhere. Use only when the user asks you to send an additional message before the final answer."
  $ tool "send_reply"
      ( optionalText "text" "Message text to send. May be omitted when image_urls is non-empty."
      , optionalTextArray "image_urls" "Image URLs to send as images in the same reply. The platform must be able to fetch these URLs."
      )
      \maybeText maybeImageUrls -> do
        context <- askToolContext
        let text = Text.strip (fromMaybe "" maybeText)
            imageUrls = filter (not . Text.null) (map Text.strip (fromMaybe [] maybeImageUrls))
            body = replyBodyWithImages text imageUrls
        if Text.null body
          then pure (argumentFailure "Either text or image_urls must be provided.")
          else do
            sent <- Chat.replyTo context.message body
            case rights sent of
              messageIds@(_:_) -> do
                let sentText = show messageIds :: String
                pure (toolText [i|Sent message ids: #{sentText}|])
              [] ->
                let err = Text.intercalate "\n" (lefts sent)
                 in
                pure (toolFailure Failure
                  { category = ExternalServiceUnavailable
                  , userMessage = [i|发送消息失败：#{err}|]
                  , detail = err
                  })

sendFileTool :: Chat.Chat :> es => Tool (Eff es)
sendFileTool =
  tagged [chatTag]
  . noisy
  . allowWhen superuserOnly
  . withDescriptionBy (\context ->
      "Send a local file to the same chat as the current user message. "
        <> case context.message.platform of
          PlatformQQ ->
            "First move or copy the file into the NapCat container, then pass its path inside that container. Do not pass the host path."
          _ ->
            "The path must be readable by the bot."
        <> " Use only when the user explicitly asks you to send a file.")
  $ tool "send_file"
      (validateArgument validFilePath
        (requiredText "path" "Local file path to send. A file:// prefix is accepted and stripped before upload."))
      \path -> do
        context <- askToolContext
        result <- Chat.uploadFile context.message path
        case result of
          Right sent -> do
            let sentText = show sent :: String
            pure (toolText [i|Sent file #{Text.pack path}; message id: #{sentText}|])
          Left err -> do
            let failureText = "发送文件失败：" <> err
            void $ Chat.replyTo context.message failureText
            pure (toolFailure Failure
              { category = ExternalServiceUnavailable
              , userMessage = failureText
              , detail = err
              })

mentionUserTool :: Chat.Chat :> es => Tool (Eff es)
mentionUserTool =
  tagged [chatTag]
  . withDescription "Send a reply in the current chat that mentions the given platform user id. Matrix user ids are textual, for example @user:server."
  $ tool "mention_user"
      ( userIdArgument "Platform user id to mention."
      , requiredText "text" "Message text to send after the mention."
      )
      \userId text -> do
        context <- askToolContext
        Chat.mentionUser context.message userId text >>= \case
          Right sent -> do
            let sentText = show sent :: String
            pure (toolText [i|Sent mention message id: #{sentText}|])
          Left err ->
            pure (toolFailure Failure
              { category = ExternalServiceUnavailable
              , userMessage = [i|发送提及消息失败：#{err}|]
              , detail = err
              })

senderMemberInfoTool :: Chat.Chat :> es => Tool (Eff es)
senderMemberInfoTool =
  tagged [chatTag]
  . withDescription "Get platform-provided member information for the sender of the current message in the current group chat."
  $ tool "sender_info" noArguments do
      context <- askToolContext
      info <- Chat.getSenderMemberInfo context.message
      pure (toolText (maybe "No member information is available for this message." jsonText info))

memberInfoTool :: Chat.Chat :> es => Tool (Eff es)
memberInfoTool =
  tagged [chatTag]
  . withDescription "Get platform-provided member information for any user id in the current group chat."
  $ tool "member_info"
      (userIdArgument "Platform user id to query in the current group.")
      \userId -> do
        context <- askToolContext
        info <- Chat.getMemberInfo context.message userId
        pure (toolText (maybe "No member information is available for this user in the current chat." jsonText info))

userAvatarTool :: (Chat.Chat :> es, KatipE :> es) => Tool (Eff es)
userAvatarTool =
  tagged [chatTag]
  . withDescription "Get avatar information for a platform user id and send the avatar image to the current chat."
  $ tool "user_avatar"
      (userIdArgument "Platform user id to query. Use message_info first when the target is the current sender or a mentioned user. 0 is invalid.")
      \userId -> do
        context <- askToolContext
        avatar <- Chat.getUserAvatar context.message userId
        case avatar of
          Nothing ->
            pure (toolText "No avatar is available for this user on this platform.")
          Just value ->
            userAvatarResult context value

listGroupMembersTool :: Chat.Chat :> es => Tool (Eff es)
listGroupMembersTool =
  tagged [chatTag]
  . withDescription "List members in the current group chat, including platform user ids and nicknames when available. QQ groups are supported. Telegram Bot API does not expose full member lists, so Telegram may return unavailable."
  $ tool "group_members" noArguments do
      context <- askToolContext
      members <- Chat.listGroupMembers context.message
      pure (toolText (maybe "Group member listing is not available for this platform or chat." jsonText members))

currentMessageInfoTool :: Tool (Eff es)
currentMessageInfoTool =
  tagged [chatTag]
  . withDescription "Return structured metadata for the current message, including platform, chat, sender, message ids, mentions, image URLs, and text."
  $ tool "message_info" noArguments do
      context <- askToolContext
      pure (toolText (jsonText (currentMessageInfoValue context.message)))

currentMessageInfoValue :: IncomingMessage -> Aeson.Value
currentMessageInfoValue message =
  Aeson.object
    [ "platform" Aeson..= chatPlatformKey message.platform
    , "chat_kind" Aeson..= (show message.kind :: Text)
    , "chat_id" Aeson..= message.chatId
    , "chat_aliases" Aeson..= message.chatAliases
    , "message_id" Aeson..= message.messageId
    , "reply_to_message_id" Aeson..= message.replyToMessageId
    , "sender_id" Aeson..= message.senderId
    , "sender_username" Aeson..= message.senderUsername
    , "mentions" Aeson..= Aeson.object
        [ "user_ids" Aeson..= message.mentions
        , "text_user_ids" Aeson..= message.mentionUsernames
        ]
    , "image_urls" Aeson..= message.imageUrls
    , "text" Aeson..= message.text
    ]

chatLogToolEntry :: ChatLog.ChatLogEntry -> Aeson.Value
chatLogToolEntry entry =
  Aeson.object
    [ "timestamp" Aeson..= entry.recordedAt
    , "chatId" Aeson..= entry.chatId
    , "senderId" Aeson..= entry.senderId
    , "senderUsername" Aeson..= entry.senderUsername
    , "messageId" Aeson..= entry.messageId
    , "imageUrls" Aeson..= entry.imageUrls
    , "text" Aeson..= entry.text
    ]

parseSenderChatLogScope :: Text -> Either Text ChatLog.SenderChatLogScope
parseSenderChatLogScope = \case
  "chat" ->
    Right ChatLog.SenderChatLogChat
  "global" ->
    Right ChatLog.SenderChatLogGlobal
  _ ->
    Left "scope must be chat or global."

validSenderLogLimit :: Int -> Either Text Int
validSenderLogLimit limit
  | limit < 0 =
      Left "limit must be >= 0."
  | limit > 100 =
      Left "limit must be <= 100."
  | otherwise =
      Right limit

chatLogTimeRange
  :: Maybe UTCTime
  -> Maybe UTCTime
  -> Either Text ChatLog.ChatLogTimeRange
chatLogTimeRange since before
  | maybe False (uncurry (>=)) ((,) <$> since <*> before) =
      Left "since must be earlier than before."
  | otherwise =
      Right ChatLog.ChatLogTimeRange{since, before}

currentSenderChatLogScopeError :: ChatLog.SenderChatLogScope -> IncomingMessage -> Maybe Text
currentSenderChatLogScopeError scope message
  | isNothing message.senderId =
      Just "Current message has no sender_id; cannot query sender-scoped chat log."
  | scope == ChatLog.SenderChatLogChat
  , isNothing message.chatId =
      Just "Current message has no chat_id; cannot query chat-scoped chat log."
  | otherwise =
      Nothing

userIdArgument :: Text -> ToolArgument Text
userIdArgument description =
  mapArgument
    (parseUserIdValue >=> validateUserId)
    (requiredArgument (fieldText "user_id" description))

parseUserIdValue :: Aeson.Value -> AesonTypes.Parser Text
parseUserIdValue value =
  (Text.strip <$> Aeson.parseJSON value)
    <|> (Text.pack . show <$> (Aeson.parseJSON value :: AesonTypes.Parser Integer))

validateUserId :: Text -> AesonTypes.Parser Text
validateUserId userId
  | Text.null (Text.strip userId) =
      fail "user_id must not be empty."
  | Text.strip userId == "0" =
      fail "user_id must not be 0."
  | otherwise =
      pure (Text.strip userId)

avatarUrl :: Aeson.Value -> Maybe Text
avatarUrl =
  AesonTypes.parseMaybe $
    Aeson.withObject "user avatar" (Aeson..: Key.fromText "avatar_url")

userAvatarResult :: (Chat.Chat :> es, KatipE :> es) => Context -> Aeson.Value -> Eff es ToolResult
userAvatarResult context value =
  case avatarUrl value of
    Nothing ->
      pure (toolText (jsonText value))
    Just url -> do
      let body = ReplyBody.imageDirective url
      sent <- Chat.replyTo context.message body
      logInfo [i|user_avatar sent avatar image: url=#{url} message_id=#{show sent :: Text}|]
      pure (toolTextWithImages (jsonText value) [url])

validFilePath :: Text -> Either Text FilePath
validFilePath rawPath
  | Text.null (Text.strip path) =
      Left "path must not be empty."
  | otherwise =
      Right (Text.unpack path)
  where
    stripped = Text.strip rawPath
    path = fromMaybe stripped (Text.stripPrefix "file://" stripped)

argumentFailure :: Text -> ToolResult
argumentFailure err =
  toolFailure (permanentArgumentFailure err err)

replyBodyWithImages :: Text -> [Text] -> Text
replyBodyWithImages text imageUrls =
  Text.strip $ Text.unlines $
    [ text | not (Text.null text) ]
      <> map ReplyBody.imageDirective imageUrls
